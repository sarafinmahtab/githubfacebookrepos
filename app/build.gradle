plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'realm-android'
}

// Load keystore
def keystoreProperties = new Properties()

def isCI = System.getenv("TRAVIS")
if (isCI) {
    keystoreProperties.setProperty("keyAlias", System.getenv("keyAlias"))
    keystoreProperties.setProperty("keyPassword", System.getenv("keyPassword"))
    keystoreProperties.setProperty("storeFile", System.getenv("storeFile"))
    keystoreProperties.setProperty("storePassword", System.getenv("storePassword"))
} else {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.android.githubfacebookrepos"
    compileSdkVersion 34

    defaultConfig {
        applicationId "com.android.githubfacebookrepos"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    buildFeatures {
        dataBinding true
    }

    signingConfigs {
        debug {
            storeFile file("keys/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
//            def flavor = variant.productFlavors[0].name.capitalize()
            def version = variant.versionName
//            def buildType = variant.buildType.name
//            def date = new Date()
//            def formattedDate = date.format('dd MMM, yy')

            outputFileName = "GithubFacebookRepos v${version}.apk"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            signingConfig signingConfigs.debug
        }

        release {
            // Disabled proguard
//            useProguard false
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        // Flag to enable support for the new java language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.google.dagger:dagger:2.37'
    kapt 'com.google.dagger:dagger-compiler:2.37'

    implementation 'com.github.bumptech.glide:glide:4.14.2'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    implementation "io.realm:realm-gradle-plugin:$realmVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    androidTestImplementation 'org.mockito:mockito-android:3.11.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
