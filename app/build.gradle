apply plugin: 'com.android.application'
apply plugin: 'realm-android'

// Load keystore
def keystoreProperties = new Properties()

def isCI = System.getenv("TRAVIS")
if (isCI) {
    keystoreProperties.setProperty("keyAlias", System.getenv("keyAlias"))
    keystoreProperties.setProperty("storeFile", System.getenv("storeFile"))
    keystoreProperties.setProperty("storeFile", System.getenv("storeFile"))
    keystoreProperties.setProperty("storePassword", System.getenv("storePassword"))
} else {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.android.githubfacebookrepos"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true
    }

    buildFeatures {
        dataBinding true
    }

    signingConfigs {

        debug {
            storeFile file("keys/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
//            def flavor = variant.productFlavors[0].name.capitalize()
            def version = variant.versionName
//            def buildType = variant.buildType.name
//            def date = new Date()
//            def formattedDate = date.format('dd MMM, yy')

            outputFileName = "GithubFacebookRepos v${version}.apk"
        }
    }

    buildTypes {

        debug {
            minifyEnabled false

            signingConfig signingConfigs.debug
        }

        release {
            // Disabled proguard
//            useProguard false
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        // Flag to enable support for the new java language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.google.android.material:material:1.2.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.google.dagger:dagger:2.28.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.28.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.squareup.retrofit2:retrofit:2.6.4'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.4'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.4'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.2'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    androidTestImplementation 'org.mockito:mockito-android:3.3.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
